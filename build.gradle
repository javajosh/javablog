plugins {
  id('java')
  id('org.springframework.boot') version('3.4.3')
  id('io.spring.dependency-management') version('1.1.7')
  id('com.diffplug.spotless') version('7.0.2')
  // SBOM support - don't run it until you need it
  // id('org.cyclonedx.bom') version('2.2.0')
}

group = 'com.javajosh'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(23)
  }
}

spotless {
  format('misc') {
    target('*.gradle', '.gitattributes', '.gitignore')
    trimTrailingWhitespace()
    leadingTabsToSpaces(2)
    endWithNewline()
  }
  java {
    googleJavaFormat()
    formatAnnotations()
//		licenseHeader('/* (C)$YEAR */')
  }
}

tasks.register('installGitHooks', Copy) {
  from(file("$rootDir/.githooks/pre-commit"))
  into(file("$rootDir/.git/hooks"))
  filePermissions{
    unix("rwxr-xr-x")
  }
}

build.dependsOn(installGitHooks)

repositories {
  mavenCentral()
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter-actuator')
  implementation('org.springframework.boot:spring-boot-starter-data-jpa')
  implementation('org.springframework.boot:spring-boot-starter-web')


  // Explore: two interesting libraries
  //  implementation 'com.fasterxml.jackson.core:jackson-databind'
  // implementation('org.springframework.boot:spring-boot-starter-quartz')
  // implementation('org.springframework.boot:spring-boot-starter-websocket')
  // implementation('io.github.wimdeblauwe:htmx-spring-boot:4.0.1')

  runtimeOnly('org.postgresql:postgresql')

  testImplementation('org.springframework.boot:spring-boot-starter-test')
  testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

tasks.named('test') {
  useJUnitPlatform()
}
